defmodule Jido.MCP do
  @moduledoc """
  Public API for calling MCP servers through direct Anubis client integration.
  """

  alias Jido.MCP.{ClientPool, Response}

  @type endpoint_id :: atom()
  @type result :: {:ok, map()} | {:error, map()}

  @spec list_tools(endpoint_id(), keyword()) :: result()
  def list_tools(endpoint_id, opts \\ []) when is_atom(endpoint_id) do
    execute(endpoint_id, "tools/list", opts, fn client, call_opts ->
      Anubis.Client.Base.list_tools(client, call_opts)
    end)
  end

  @spec call_tool(endpoint_id(), String.t(), map(), keyword()) :: result()
  def call_tool(endpoint_id, tool_name, arguments \\ %{}, opts \\ [])
      when is_atom(endpoint_id) and is_binary(tool_name) and is_map(arguments) do
    execute(endpoint_id, "tools/call", opts, fn client, call_opts ->
      Anubis.Client.Base.call_tool(client, tool_name, arguments, call_opts)
    end)
  end

  @spec list_resources(endpoint_id(), keyword()) :: result()
  def list_resources(endpoint_id, opts \\ []) when is_atom(endpoint_id) do
    execute(endpoint_id, "resources/list", opts, fn client, call_opts ->
      Anubis.Client.Base.list_resources(client, call_opts)
    end)
  end

  @spec list_resource_templates(endpoint_id(), keyword()) :: result()
  def list_resource_templates(endpoint_id, opts \\ []) when is_atom(endpoint_id) do
    execute(endpoint_id, "resources/templates/list", opts, fn client, call_opts ->
      Anubis.Client.Base.list_resource_templates(client, call_opts)
    end)
  end

  @spec read_resource(endpoint_id(), String.t(), keyword()) :: result()
  def read_resource(endpoint_id, uri, opts \\ []) when is_atom(endpoint_id) and is_binary(uri) do
    execute(endpoint_id, "resources/read", opts, fn client, call_opts ->
      Anubis.Client.Base.read_resource(client, uri, call_opts)
    end)
  end

  @spec list_prompts(endpoint_id(), keyword()) :: result()
  def list_prompts(endpoint_id, opts \\ []) when is_atom(endpoint_id) do
    execute(endpoint_id, "prompts/list", opts, fn client, call_opts ->
      Anubis.Client.Base.list_prompts(client, call_opts)
    end)
  end

  @spec get_prompt(endpoint_id(), String.t(), map(), keyword()) :: result()
  def get_prompt(endpoint_id, prompt_name, arguments \\ %{}, opts \\ [])
      when is_atom(endpoint_id) and is_binary(prompt_name) and is_map(arguments) do
    execute(endpoint_id, "prompts/get", opts, fn client, call_opts ->
      Anubis.Client.Base.get_prompt(client, prompt_name, arguments, call_opts)
    end)
  end

  @spec refresh_endpoint(endpoint_id()) :: result()
  def refresh_endpoint(endpoint_id) when is_atom(endpoint_id) do
    with {:ok, endpoint, _ref} <- ClientPool.refresh(endpoint_id),
         {:ok, status} <- ClientPool.endpoint_status(endpoint_id) do
      {:ok,
       %{
         status: :ok,
         endpoint: endpoint_id,
         method: "endpoint/refresh",
         data: %{
           endpoint: endpoint,
           status: status
         }
       }}
    end
  end

  @spec endpoint_status(endpoint_id()) :: {:ok, map()} | {:error, term()}
  def endpoint_status(endpoint_id) when is_atom(endpoint_id) do
    ClientPool.endpoint_status(endpoint_id)
  end

  defp execute(endpoint_id, method, opts, fun) do
    with {:ok, endpoint, ref} <- ClientPool.ensure_client(endpoint_id) do
      call_opts = Keyword.put_new(opts, :timeout, endpoint.timeouts.request_ms)
      response = fun.(ref.client, call_opts)
      Response.normalize(endpoint_id, method, response)
    end
  end
end
